apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

def properties = project.rootProject.file('local.properties').withDataInputStream { stream ->
    def props = new Properties()
    props.load(stream)
    props
}

group = project.ext['GROUP']
version = android.defaultConfig.versionName

publishing {
    publications {
        Production(MavenPublication) {

            artifact "$buildDir/outputs/aar/library-release.aar"
            groupId this.group
            artifactId project.ext['ARTIFACT_ID']
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each { Dependency dependency ->
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (dependency.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')

                        dependencyNode.appendNode('groupId', dependency.group)
                        dependencyNode.appendNode('artifactId', dependency.name)
                        dependencyNode.appendNode('version', dependency.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = properties['BINTRAY_USER']
    key = properties['BINTRAY_API_KEY']
    publications = ['Production']
    configurations = ['archives']
    publish = true
    override = true
    pkg {
        repo = 'maven'
        name = project.ext['POM_NAME']
        description = project.ext['POM_DESCRIPTION']
        publish = true
        override = true
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = project.ext['POM_VCS_URL']
    }
}